import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'war'
apply from: 'provided.gradle'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.2.1'
    }
}

apply plugin: 'com.bmuschko.cargo'
apply plugin: 'com.bmuschko.cargo-base'

dependencies {
    compile(
            libraries.'mysql',
            libraries.'druid',
            libraries.'java-jwt',
            libraries.'cors-filter',
            libraries.'freemarker',
            libraries.'commons-lang3'
    )
    compile group: 'com.google.zxing', name: 'core', version: '3.2.1'
}

cargo {
    containerId = 'tomcat8x'
    port = 10080 //服务器tomcat端口号
    deployable {
        context = '/'
    }
}

task deploy(type: com.bmuschko.gradle.cargo.tasks.remote.CargoRedeployRemote) {
    dependsOn clean, war
    hostname = "api.pickpick.io"//服务器地址
    username = "admin" //来源于tomcat-users.xml
    password = "abcd1234"
    logger.lifecycle("CargoDeployRemote war package!")
}

def loadConfiguration() {
    def environment = hasProperty('env') ? env : 'dev'
    project.ext.envrionment = environment
    println "Environment is set to $environment"

    def configFile = file('environment.groovy')
    def config = new ConfigSlurper("$environment").parse(configFile.toURL())
    project.ext.config = config
    config.toProperties()
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter(ReplaceTokens,
                tokens: loadConfiguration()
        )
    }
}

